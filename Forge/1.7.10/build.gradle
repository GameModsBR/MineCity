buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

project(':Forge:1.7.10') {
    archivesBaseName = "MineCity-Forge-MC1.7.10"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

minecraft {
    version = "1.7.10-10.13.4.1566-1.7.10"
    runDir = "run"
    srgExtra "PK: org/mcstats br/com/gamemods/minecity/forge/mcstats"
}

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    maven { url = "http://maven.cil.li/" }
    maven { url = "http://maven.ic2.player.to/" }
}

dependencies {
    shade project(':Core')
    shade project(':Forge:Base')
    shade "org.mcstats.forge:metrics:R8-SNAPSHOT"
    compile "mysql:mysql-connector-java:5.1.32"
    compile "li.cil.oc:OpenComputers:MC1.7.10-1.6.+:api"
    compile "net.industrial-craft:industrialcraft-2:2.2.825-experimental:dev"
    compile "com.github.GameModsBR:UniversalCoinsServer:-SNAPSHOT"
    testCompile fileTree(dir: "run/mods", include: ["*.jar"], exclude:["*Assets*"])
    testCompile fileTree(dir: "run/mods/1.7.10", include: ["*.jar"])
}

jar {
    manifest {
        attributes (
                'FMLCorePlugin': 'br.com.gamemods.minecity.forge.mc_1_7_10.core.MineCitySevenCoreMod',
                'FMLCorePluginContainsFMLMod': 'true',
                'FMLAT': 'minecity_at.cfg'
        )
    }

    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            include 'br/**', '*.yml', '*.info', 'assets/minecity/**', 'org/mcstats/**'
        }
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
